product Module {
    declarations: *TopLevelDeclaration
}

branch TopLevelDeclaration {
    Function
}

// 関数定義
product Function {
    "fn" name: Identifier "(" 
        params: *Parameter with "comma_separated_params"
    ")" return_type: ?TypeLiteral
        body: BlockExpression
}

product Parameter {
    name: Identifier
}

product BlockExpression {
    "{"
        statements: *Statement
        final_expr: ?Expression
    "}"
}

branch Statement {
    ExpressionStmt
    Assignment
    Return
}

product ExpressionStmt {
    expr: Expression
    ";"
}

product Assignment {
    "let" name: Identifier "=" value: Expression ";"
}

product Return {
    "return" value: ?Expression ";"
}

branch Expression {
    LiteralAST
    FunctionCall
}

product LiteralAST {
    value: StringLiteral
}

product FunctionCall {
    name: Identifier "("
        args: *Expression with "comma_separated_exprs"
    ")"
}

branch TypeLiteral {
    IntType
}

product IntType {
    "Int"
}

product Identifier

product StringLiteral
